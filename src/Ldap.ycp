/**
 * File:
 *   modules/Ldapclient.ycp
 *
 * Module:
 *   Configuration of LDAP client
 *
 * Summary:
 *   LDAP client configuration data, I/O functions.
 *
 * Authors:
 *   Thorsten Kukuk <kukuk@suse.de>
 *   Anas Nashif <nashif@suse.de>
 *
 * $Id$
 *
 */

{
    module "Ldapclient";
    textdomain "ldap_client";

    import "Runlevel";
    import "Report";
    import "Summary";

    /**
     * Should ypbind be started at boot?
     * If not, other settings are not touched.
     */
    global boolean start = false;

    /**
     * IP addresses of LDAP server.
     */
    global string  server = "";

    global boolean auto = false;

    string domain = "";
    string old_domain = nil;
    boolean domain_changed = false;

    /* Do we have an v2 or v3 ldap server? */
    global boolean ldap_v2 = false;
    global boolean ldap_tls = false;

    /**
     * If the domain has changed from a nonempty one, it may only be
     * changed at boot time. Use this to warn the user.
     * @return whether changed by SetDomain
     */
    global define boolean DomainChanged () ``{
	return domain_changed;
    }

    /**
     * @return Get the LDAP domain.
     */
    global define string GetDomain () ``{
	return domain;
    }

    /**
     * Set the LDAP domain.
     * @param new_domain a new domain
     */
    global define void SetDomain (string new_domain) ``{
	domain = new_domain;
	if (domain != old_domain && old_domain != "")
	{
	    domain_changed = true;
	}
    }

    /* ---------------------------------------------------------------- */

    /**
     * Get all the LDAP configuration from a map.
     * When called by ldap_auto (preparing autoinstallation data)
     * the map may be empty.
     * @param settings	$["start": "domain": "servers":[...] ]
     * @return	success
     */
    global define boolean Import (map settings) ``{
	if (size (settings) == 0)
	{
	    //Provide defaults for autoinstallation editing:
	    //Leave empty.
	    old_domain = domain;
	    return true;
	}

	boolean missing = false;
	foreach (`k, ["start_ldap", "ldap_domain", "ldap_server", "ldap_v2", "ldap_tls"], ``{
	    if (! haskey (settings, k))
	    {
		y2error ("Missing at Import: '%1'.", k);
		missing = true;
	    }
	});
	if (missing)
	{
	    return false;
	}

	start = lookup (settings, "start_ldap", false);
	server = lookup (settings, "ldap_server", "");
	domain = lookup (settings, "ldap_domain", "");
	ldap_v2 = lookup (settings, "ldap_v2", false);
	ldap_tls = lookup (settings, "ldap_tls", false);
	old_domain = domain;
	return true;
    }

    /**
     * Only set variables, without checking anything
     * @return: void
     */
    global define void Set(map settings)  ``{
	start = lookup (settings, "start_ldap", false);
	server = lookup (settings, "ldap_server", "");
	domain = lookup (settings, "ldap_domain", "");
	ldap_v2 = lookup (settings, "ldap_v2", false);
	ldap_tls = lookup (settings, "ldap_tls", false);
	return;
    }
    
    /**
     * Dump the LDAP settings to a map, for autoinstallation use.
     * @return $["start":, "servers":[...], "domain":]
     */
    global define map Export () ``{
	return $[
	    "start_ldap": start,
	    "ldap_server": server,
	    "ldap_domain": domain,
	    "ldap_v2": ldap_v2,
	    "ldap_tls": ldap_tls,
	    ];
    }

    /**
     * Reads LDAP settings from the SCR
     * @return success
     */
    global define boolean Read () ``{
	start = (SCR::Execute (.target.bash, "/usr/bin/grep '^passwd:.*ldap' /etc/nsswitch.conf") == 0);
	server = SCR::Read(.etc.openldap.ldap_conf.host);
	if (server == nil) {
	  server = "";
	}
	domain  = SCR::Read(.etc.openldap.ldap_conf.base);
	if (domain == nil) {
	  domain = "";
	}
	ldap_v2 = (SCR::Read(.etc.openldap.ldap_conf.ldap_version) == "2");
	ldap_tls = (SCR::Read(.etc.openldap.ldap_conf.ssl) == "start_tls");
	old_domain = domain;
	return true;
    }

    /**
     * Only write new configuration w/o starting any scripts
     * @return true on success
     */
    global define boolean WriteOnly()``{
	return (Write(true));
    }

    /**
     * Saves LDAP configuration.
     * (No parameters because it is too short to abort)
     * @return true on success
     */
    global define boolean Write (boolean writeonly) ``{
	if (!writeonly)
	    Runlevel::RunInitScript ("ypbind", "stop");

	if (start) {
	    SCR::Write(.etc.openldap.ldap_conf.host, server);
	    SCR::Write(.etc.openldap.ldap_conf.base, domain);
	    if (ldap_v2)
	      SCR::Write(.etc.openldap.ldap_conf.ldap_version, "2");
	    else
	      SCR::Write(.etc.openldap.ldap_conf.ldap_version, "3");
	    if (ldap_tls)
	      SCR::Write(.etc.openldap.ldap_conf.ssl, "start_tls");
	    SCR::Write(.pam.all.account.pam_unix2, "+use_ldap");
	    SCR::Write(.pam.all.auth.pam_unix2, "+use_ldap");
	    SCR::Write(.pam.all.password.pam_unix2, "+use_ldap");

	    SCR::Write(.etc.nsswitch_conf.passwd, "files ldap");
	    SCR::Write(.etc.nsswitch_conf.group, "files ldap");
	    if (Runlevel::ServiceStatus ("nscd") == 0 && !writeonly) {
		Runlevel::RunInitScript ("nscd", "restart");
	    }
	} else {
	    SCR::Write(.etc.nsswitch_conf.passwd, "compat");
	    SCR::Write(.etc.nsswitch_conf.group, "compat");
	    SCR::Write(.etc.nsswitch_conf, nil);
	    SCR::Write(.pam.all.account.pam_unix2, "-use_ldap");
	    SCR::Write(.pam.all.auth.pam_unix2, "-use_ldap");
	    SCR::Write(.pam.all.password.pam_unix2, "-use_ldap");

	    if (Runlevel::ServiceStatus ("nscd") == 0 && !writeonly) {
		Runlevel::RunInitScript ("nscd", "restart");
	    }

	    SCR::Execute(.target.bash, "/bin/cp -a /var/lib/yast2-config-ldap_client/* /etc/pam.d");
	    SCR::Execute(.target.bash, "/bin/rm -rf /var/lib/yast2-config-ldap_client");
	}

	return true;
    }

        
    /** 
     * Summary()
     * returns html formated configuration summary
     * @return string
     */

    global define Summary ()
	``{	
	string summary = "";
	summary = Summary::AddHeader(summary, _("LDAP Client enabled"));
	summary = Summary::AddLine(summary, (start) ? "Yes" : Summary::NotConfigured());
	summary = Summary::AddHeader(summary, _("LDAP Domain"));
	summary = Summary::AddLine(summary, (domain != "") ? domain : Summary::NotConfigured());
	summary = Summary::AddHeader(summary, _("LDAP Server"));
	summary = Summary::AddLine(summary,( server!="") ? server : Summary::NotConfigured());
	summary = Summary::AddHeader(summary, _("LDAP Version 2"));
	summary = Summary::AddLine(summary, (ldap_v2) ? "Yes" : Summary::NotConfigured());
	summary = Summary::AddHeader(summary, _("LDAP &TLS/SSL"));
	summary = Summary::AddLine(summary, (ldap_tls) ? "Yes" : Summary::NotConfigured());
	
	return summary;	
    }
}
