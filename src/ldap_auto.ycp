/**
 * File:
 *   ldap_client_auto.ycp
 *
 * Package:
 *   Configuration of LDAP client
 *
 * Summary:
 *   Client for autoinstallation
 *
 * Authors:
 *   Thorsten Kukuk <kukuk@suse.de>
 *   Anas Nashif <nashif@suse.de>
 *
 * $Id$
 *
 * This is a client for autoinstallation. It takes its arguments,
 * goes through the configuration and return the setting.
 * Does not do any changes to the configuration.
 */

/**
 * @param first a map of LDAP settings
 * @return map edited settings or an empty map if canceled
 * @example map mm = $[ "FAIL_DELAY" : "77" ];
 * @example map ret = CallModule ("ldap_auto", [ mm ]);
 */

{
    textdomain "ldap_client";

    import "Ldapclient";
    import "Wizard";
    include "ldap_client/ui.ycp";

    Ldapclient::auto = true;
    list args = Args ();
    if ( size (args) <= 0 )
    {
	y2error ("Did not get the settings, probably some mistake...");
	return false;
    }
    if ( !is ( Args (0), map ) )
    {
	y2error ("Bad argument for ldap_auto: %1", Args (0));
	return false;
    }
    
    // The settings are in the first argument
    map settings = $[];
    boolean cms_mode = false;
    {
	integer i = 0;
	while (i < size (Args()))
	{
	    if (is (Args (i), map) && nil != Args (i))	settings = Args (i);
	    else if (Args (i) == .cms)			cms_mode=true;	   
	    i = i + 1;
	}
    }
      
    y2milestone("Imported: (%1)", settings);

    Ldapclient::Import ( settings );

    define set_contents(map settings) ``{
	Ldapclient::Import ( settings );
	term contents =
	    `VBox(
		  `VSpacing(1),
		  `RichText( `id(`summary), Ldapclient::Summary()),
		  `VSpacing(),
		  `HBox(
			`PushButton(`id(`configure), _("&Configure LDAP")),
			`HStretch(),
			`PushButton(`id(`reset), _("&Reset Configuration"))
			)
				
		  );

		
	Wizard::SetContents(_("LDAP Configuration"),	
			    contents, "", true, true);
    }


    if (cms_mode) {
	import "Wizard"; 
	set_contents(settings);
	any ret = nil;
	repeat {
	    ret = UI::UserInput();
	    if (ret == `configure)
	    {
		map result = CallModule("ldap_client_auto", [settings]);
		if (result!=$[])
		    settings = result;

		set_contents(settings);
	    }
	    else if (ret == `reset) {
		settings = $[];
		set_contents(settings);
	    }
	    else if (ret == `help)
	    {
		UI::LongTextPopup (_("Help"), `RichText (_("Help")), 50, 15);
	    }
	} until ( ret == `key || ret ==`next || ret == `back);
	return [ret,settings];

    }
    
    Wizard::CreateDialog ();
    Wizard::ReplaceAbortButton(`Empty ());
    symbol ret = LdapDialog ();
    UI::CloseDialog ();

    if (ret == `next)
    {
	return Ldapclient::Export ();
    }
    return $[];
}
