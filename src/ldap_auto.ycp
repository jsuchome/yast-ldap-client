/**
 * File:	clients/ldap_auto.ycp
 * Package:	Configuration of LDAP client
 * Summary:	Client for autoinstallation
 * Authors:	Thorsten Kukuk <kukuk@suse.de>
 *		Anas Nashif <nashif@suse.de>
 *
 * $Id$
 *
 * This is a client for autoinstallation. It takes its arguments,
 * goes through the configuration and return the setting.
 * Does not do any changes to the configuration.
 */

/**
 * @param first a map of LDAP settings
 * @return map edited settings or an empty map if canceled
 * @example map mm = $[ "FAIL_DELAY" : "77" ];
 * @example map ret = WFM::CallModule ("ldap_auto", [ mm ]);
 */

{

textdomain "ldap-client";
y2milestone("----------------------------------------");
y2milestone("Ldap auto started");

import "Ldap";
import "Wizard";
import "Summary";
include "ldap/ui.ycp";


any ret = nil;
string func = "";
map param = $[];

/* Check arguments */
if(size(Args()) > 0 && is(Args(0), string)) {
    func = WFM::Args(0);
    if(size(Args()) > 1 && is(Args(1), map))
	param = WFM::Args(1);
}
y2debug("func=%1", func);
y2debug("param=%1", param);



/* Import Data*/
if(func == "Import") {
    ret = Ldap::Import(param);
}
/* Create a  summary*/
else if(func == "Summary") {
    ret = Ldap::Summary();
}
/* Reset configuration */
else if (func == "Reset") {
    Ldap::Import($[]);
    ret = $[];
}
/* Change configuration (run AutoSequence) */
else if (func == "Change") {
	Wizard::CreateDialog ();
	Wizard::ReplaceAbortButton(`Empty ());
	ret = LdapDialog ();
	UI::CloseDialog ();
}
/* Return actual state */
else if (func == "Result") {
    ret = Ldap::Export();
}
/* Write givven settings */
else if (func == "Write") {
    import "Progress";
    Ldap::write_only = true;
    Progress::off();
    ret = Ldap::Write();
    Progress::on();
}
/* Unknown function */
else {
    y2error("Unknown function: %1", func);
    ret = false;
}

y2debug("ret=%1", ret);
y2milestone("Ldap auto finished");
y2milestone("----------------------------------------");

return ret;

/* EOF */
}
