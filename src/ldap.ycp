/**
 * File:	clients/ldap.ycp
 * Module:	Configuration of LDAP client
 * Summary:	Main file
 * Authors:	Thorsten Kukuk <kukuk@suse.de>
 *		Anas Nashif <nashif@suse.de>
 *
 * $Id$
 */

/***
 * <h3>Configuration of the ldap</h3>
 */

{

    textdomain "ldap-client";

    import "Require";
    import "Popup";
    import "Ldap";
    import "RichText";

    include "ldap/wizards.ycp";

    include "commandline/commandline.ycp";

    if (!Require::RequireAndConflict (Ldap::required_packages, [],
			     // popup: requred applications (%1)
			     _("<p>To configure and access LDAP services,
the <b>%1</b> packages must be installed.<br>
Install these packages now?</p>
")))
    {
	// popup: requred applications were not installed
	Popup::Message(_("The configuration of the LDAP client
cannot be completed without the mentioned packages."));
	return `auto;
    }

// --------------------------------------------------------------------------
// --------------------------------- cmd-line handlers

/**
 * Enable or disable LDAP authentication
 * @param options  a list of parameters passed as args
 * @return boolean true on success
 */
global define boolean LdapEnableHandler( map options ) ``{
    // check the "command" to be present exactly once
    string command = CommandLine::UniqueOption( options,
	["enable", "disable" ] );
    if( command == nil ) return false;

    Ldap::start = ( command == "enable" );
    return true;
}

/**
 * Print summary of basic options
 * @return boolean false
 */
global define boolean LdapSummaryHandler (map options ) ``{

    CommandLine::Print (RichText::Rich2Plain ("<br>" + Ldap::ShortSummary ()));
    return false; // do not call Write...
}

/**
 * Change basic configuration of LDAP client (server, base DN)
 * @param options  a list of parameters passed as args
 * @return boolean true on success
 */
global define boolean LdapChangeConfiguration (map options ) ``{

    boolean ret = false;
    string server = options["server"]:nil;
    if( server != nil )
    {
	Ldap::server = server;
	ret = true;
    }
    string base = options["base"]:nil;
    if( base != nil )
    {
	Ldap::SetDomain (base);
	ret = true;
    }
    return ret;
}

global define boolean LdapWrite () ``{

    return (Ldap::Write(``{ return false; }) == `next);
}

/* the command line description map */
map cmdline = $[
    "id"		: "ldap",
    // translators: command line help text for Ldap client module
    "help"		: _("LDAP client configuration module."),
    "guihandler"	: ``(LdapSequence()),
    "initialize"	: ``(Ldap::Read()),
    "finish"		: ``(LdapWrite()),
    "actions"		: $[
	"pam" :$[
	    "handler"	: ``(LdapEnableHandler()),
	    // translators: command line help text for pam action
	    "help"	: _("Enable or disable authentication with LDAP")
	],
	"summary" :$[
	    "handler"	: ``(LdapSummaryHandler()),
	    // translators: command line help text for summary action
	    "help"	: _("Configuration summary of LDAP client")
	],
	"configure"	: $[
	    "handler"	: ``(LdapChangeConfiguration()),
	    // translators: command line help text for configure action
	    "help"	: _("Change the global settings of LDAP client")
	]
    ],
    "options"		: $[
	"enable"	:$[
	    // translators: command line help text for pam enable option
	    "help"	: _("Enable the service")
	],
	"disable"	:$[
	    // translators: command line help text for pam disable option
	    "help"	: _("Disable the service")
	],
	"server"	:$[
	    // translators: command line help text for the server option
	    "help"	: _("The LDAP server name"),
	    "type"	: "string"
	],
	"base"	:$[
	    // translators: command line help text for the base option
	    "help"	: _("Distinguished Name (DN) of the search base"),
	    "type"	: "string"
	],
    ],
    "mappings"		: $[
	"pam"		: [ "enable", "disable" ],
	"summary"	: [],
	"configure"	: [ "server", "base" ],
    ]
];

   return CommandLineRun( cmdline );
}
